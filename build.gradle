apply plugin: 'java'
apply plugin: 'application'

repositories {
    mavenCentral()
}

def javafxVersion = '21'

def os = org.gradle.internal.os.OperatingSystem.current()
def arch = System.getProperty("os.arch")
def platform

if (os.isWindows()) {
    platform = 'win'
} else if (os.isMacOsX()) {
    platform = arch.contains("aarch64") ? "mac-aarch64" : "mac"
} else if (os.isLinux()) {
    platform = arch.contains("aarch64") ? "linux-aarch64" : "linux"
} else {
    platform = 'linux'
}

mainClassName = 'CrossyRoadApp'

sourceSets {
    main {
        java {
            srcDirs = ['src'] 
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

dependencies {
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-media:${javafxVersion}:${platform}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
}

test {
    useJUnitPlatform()
}

run {
    jvmArgs = [
        "--module-path", classpath.asPath,
        "--add-modules", "javafx.controls,javafx.graphics,javafx.media"
    ]
}

